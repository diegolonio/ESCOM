/*
 * Apolonio Villegas Diego Armando
 * Palacios Cabrera Alberto
 * 
 * Práctica 5
 *
 */
	.DEF TMP = R16
	.DEF UNIDADES = R17
	.DEF DECENAS = R18
	.DEF CENTENAS = R19
	.DEF DISPLAY_UNIDADES = R20
	.DEF DISPLAY_DECENAS = R21
	.DEF DISPLAY_CENTENAS = R22
	.DEF MUX = R23

	.CSEG

	.ORG $00
	JMP PROGRAMA

	.ORG $0002
	JMP CONTEO_CICLOS

	.ORG $0016
	JMP CARGAR_CONTEO

	.ORG $0020
	JMP DISPLAYS

; Codificación de los números para el display de 7 segmentos
	.DB $7B, $0A, $B3, $9B, $CA, $D9, $F9, $0B, $FB, $CB, $80, $00

PROGRAMA:
; Configuración puerto D
	LDI TMP, $FB
	OUT DDRD, TMP
; Configuración puerto C
	LDI TMP, $1F
	OUT DDRC, TMP
; Configuración de la interrupción externa 0
	SEI
	LDI TMP, $01
	OUT EIMSK, TMP
	LDI TMP, $03
	STS EICRA, TMP
; Configuración del timer 0
	LDI TMP, $04
	OUT TCCR0B, TMP
	LDI TMP, $01
	STS TIMSK0, TMP
; Configuración del timer 1 comparador A
	LDI TMP, $0C
	STS TCCR1B, TMP
	LDI TMP, $F4
	STS OCR1AH, TMP
	LDI TMP, $24
	STS OCR1AL, TMP
	LDI TMP, $02
	STS TIMSK1, TMP
; Inicialización de variables
	LDI UNIDADES, $00
	LDI DECENAS, $00
	LDI CENTENAS, $00
	LDI MUX, $06
; Cargar las codificaciones del display en la memoria de datos
	LDI ZL, $44
	LDI ZH, $00
	LDI XL, $00
	LDI XH, $01
CARGAR_CODIFICACIONES:
	CPI ZL, $4F
	BREQ MAIN
	LPM TMP, Z+
	ST X+, TMP
	JMP CARGAR_CODIFICACIONES

MAIN:
	JMP MAIN

CONTEO_CICLOS:
	INC UNIDADES
	CPI UNIDADES, $0A
	BREQ INCREMENTAR_DECENAS
	RETI

INCREMENTAR_DECENAS:
	LDI UNIDADES, $00
	INC DECENAS
	CPI DECENAS, $0A
	BREQ INCREMENTAR_CENTENAS
	RETI
	
INCREMENTAR_CENTENAS:
	LDI DECENAS, $00
	INC CENTENAS
	CPI CENTENAS, $0A
	BREQ MENSAJE_ERROR
	RETI

MENSAJE_ERROR:
	LDI UNIDADES, $0A
	LDI DECENAS, $0A
	LDI CENTENAS, $0A
	RETI

CARGAR_CONTEO:
	MOV DISPLAY_UNIDADES, UNIDADES
	MOV DISPLAY_DECENAS, DECENAS
	MOV DISPLAY_CENTENAS, CENTENAS
	LDI UNIDADES, $00
	LDI DECENAS, $00
	LDI CENTENAS, $00
	RETI

DISPLAYS:
	LDI TMP, $07
	OUT PORTD, TMP
	CPI MUX, $06
	BRNE ENCENDER_DECENAS
	MOV XL, DISPLAY_UNIDADES
	LD TMP, X
	OUT PORTD, TMP
	OUT PORTC, MUX
	LDI MUX, $05
	RETI
	
ENCENDER_DECENAS:
	LDI TMP, $07
	OUT PORTD, TMP
	CPI MUX, $05
	BRNE ENCENDER_CENTENAS
	MOV XL, DISPLAY_DECENAS
	LD TMP, X
	OUT PORTD, TMP
	OUT PORTC, MUX
	LDI MUX, $03
	RETI

ENCENDER_CENTENAS:
	LDI TMP, $07
	OUT PORTD, TMP
	MOV XL, DISPLAY_CENTENAS
	LD TMP, X
	OUT PORTD, TMP
	OUT PORTC, MUX
	LDI MUX, $06
	RETI
